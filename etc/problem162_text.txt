
Formulas from the Excel solution to Problem 162.

Column B: =LET(COUNT_FUNCTION, LAMBDA(W,Z,O,A, (MULTINOMIAL(W, Z, O, A) - IF(Z > 0, MULTINOMIAL(W, Z - 1, O, A), 0)) * 13 ^ W), FIX, LAMBDA(F, LAMBDA(X, F(LAMBDA(V, X(X)(V))))(LAMBDA(X, F(LAMBDA(V, X(X)(V)))))), ALOOP, FIX(LAMBDA(G, LAMBDA(W, LAMBDA(Z, LAMBDA(O, LAMBDA(A, IF(W + Z + O + A <= 16, COUNT_FUNCTION(W, Z, O, A) + G(W)(Z)(O)(A + 1), 0))))))), OLOOP, FIX(LAMBDA(G, LAMBDA(W, LAMBDA(Z, LAMBDA(O, IF(W + Z + O <= 16, ALOOP(W)(Z)(O)(1) + G(W)(Z)(O + 1), 0)))))), ZLOOP, FIX(LAMBDA(G, LAMBDA(W, LAMBDA(Z, IF(W + Z <= 16, OLOOP(W)(Z)(1) + G(W)(Z + 1), 0))))), ZLOOP(A_)(1))
Column C: =LET(FIX, LAMBDA(F, LAMBDA(X, F(LAMBDA(V, X(X)(V))))(LAMBDA(X, F(LAMBDA(V, X(X)(V)))))), MUL, FIX(LAMBDA(G, LAMBDA(N, LAMBDA(TIMES, IF(TIMES <= 0, N, G(MOD(13 * N, 16^6))(TIMES - 1)))))), COUNT_FUNCTION, LAMBDA(W,Z,O,A, MUL(MOD(ROUND(MULTINOMIAL(W, Z, O, A), 0) - IF(Z > 0, ROUND(MULTINOMIAL(W, Z - 1, O, A), 0), 0), 16^6))(W)), ALOOP, FIX(LAMBDA(G, LAMBDA(W, LAMBDA(Z, LAMBDA(O, LAMBDA(A, IF(W + Z + O + A <= 16, MOD(MOD(COUNT_FUNCTION(W, Z, O, A), 16^6) + G(W)(Z)(O)(A + 1), 16^6), 0))))))), OLOOP, FIX(LAMBDA(G, LAMBDA(W, LAMBDA(Z, LAMBDA(O, IF(W + Z + O <= 16, MOD(ALOOP(W)(Z)(O)(1) + G(W)(Z)(O + 1), 16^6), 0)))))), ZLOOP, FIX(LAMBDA(G, LAMBDA(W, LAMBDA(Z, IF(W + Z <= 16, MOD(OLOOP(W)(Z)(1) + G(W)(Z + 1), 16^6), 0))))), ZLOOP(A3)(1))
Column B Summation: =DEC2HEX(FLOOR(SUM(B3:B19)/(16^6), 1))
Column C Summation: =DEC2HEX(MOD(SUM(C3:C19), 16^6))
